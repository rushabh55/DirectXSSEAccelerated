/***************************************************************
pVolumeRender.hlsl: Renders a 3D texture using ray-marching

Author: Valentin Hinov
Date: 23/02/2014
***************************************************************/

// Constant buffers
cbuffer BufferPerFrame : register (b0) {
	float3 vEyePos;	
	float  padding1;	// 16 bytes
};

cbuffer BufferPerObject : register (b1) {
	float3 vTranslate;  
	float  padding2;	// 16 bytes

	float3 vScale;
	float  padding3;	// 32 bytes
};

cbuffer SmokePropertiesBuffer : register (b2) {
	float4 vSmokeColor;

	float fSmokeAbsorption;
	float fFireAbsorption;
	int iNumSamples;
	float padding;		// 32 bytes
};

Texture3D<float> volumeValues : register (t0);
Texture3D<float> reactionValues : register (t1);
Texture2D fireGradient : register(t2);

// TODO - replace with point sampler?
SamplerState linearSampler : register (s0);

struct PixelInputType {
	float4 position : SV_POSITION;
	float3 worldPosition : TEXCOORD0;
};

struct Ray {
	float3 origin;
	float3 dir;
};
			
struct AABB {
	float3 Min;
	float3 Max;
};

// find intersection points of a ray with a box
bool IntersectBox(Ray r, AABB aabb, out float t0, out float t1) {
	float3 invR = 1.0f / r.dir;
	float3 tbot = invR * (aabb.Min-r.origin);
	float3 ttop = invR * (aabb.Max-r.origin);
	float3 tmin = min(ttop, tbot);
	float3 tmax = max(ttop, tbot);
	float2 t = max(tmin.xx, tmin.yz);
	t0 = max(t.x, t.y);
	t = min(tmax.xx, tmax.yz);
	t1 = min(t.x, t.y);
	return t0 <= t1;
}

float SampleDensity(float3 uv) {
	return volumeValues.SampleLevel(linearSampler, uv, 0);
}

float SampleReaction(float3 uv) {
	return reactionValues.SampleLevel(linearSampler, uv, 0);
}

void CommonCalculations(PixelInputType input, out float3 start, out float3 stepVector, out float stepSize) {
	float3 pos = vEyePos;

	Ray r;
	r.origin = pos;
	r.dir = normalize(input.worldPosition - pos);

	AABB aabb;
	aabb.Min = float3(-0.5f, -0.5f, -0.5f) * vScale + vTranslate;
	aabb.Max = float3(0.5f, 0.5f, 0.5f) * vScale + vTranslate;

	//figure out where ray from eye hit front of cube
	float tnear, tfar;
	IntersectBox(r, aabb, tnear, tfar);

	//if eye is in cube then start ray at eye
	if (tnear < 0.0f) {
		tnear = 0.0f;
	}

	float3 rayStart = r.origin + r.dir * tnear;
	float3 rayStop = r.origin + r.dir * tfar;

	//convert to texture space
	rayStart -= vTranslate;
	rayStop -= vTranslate;
	float3 halfScale = 0.5f * vScale;
	rayStart = (rayStart + halfScale) / vScale;
	rayStop = (rayStop + halfScale) / vScale;

	float dist = distance(rayStop, rayStart);
	stepSize = dist / float(iNumSamples);
	start = rayStart;
	stepVector = normalize(rayStop - rayStart) * stepSize;
}

////////////////////////////////////////////////////////////////////////////////
// Pixel Shaders
////////////////////////////////////////////////////////////////////////////////
float4 SmokeVolumeRenderPixelShader(PixelInputType input) : SV_TARGET {
	float3 start, ds;
	float stepSize;
	CommonCalculations(input, start, ds, stepSize);

	float alpha = 1.0f;

	for(int i = 0; i < iNumSamples; ++i, start += ds) {	 
		float D = SampleDensity(start);	
		alpha *= 1.0f - saturate(D * stepSize * fSmokeAbsorption);		

		if (alpha <= 0.01f) {
			break;
		}
	}
	
	return vSmokeColor * (1.0f - alpha);
}

float4 FireVolumeRenderPixelShader(PixelInputType input) : SV_TARGET {
	float3 start, ds;
	float stepSize;
	CommonCalculations(input, start, ds, stepSize);

	float smokeAlpha = 1.0f;
	float fireAlpha = 1.0f;

	for(int i = 0; i < iNumSamples; ++i, start += ds) {	 
		float D = SampleDensity(start);	
		float R = SampleReaction(start);
		smokeAlpha *= 1.0f - saturate(D * stepSize * fSmokeAbsorption);		
		fireAlpha *= 1.0f - saturate(R * stepSize * fFireAbsorption);		

		if (smokeAlpha <= 0.01f && fireAlpha <= 0.01f) {
			break;
		}
	}
	float4 smoke = vSmokeColor * (1.0f - smokeAlpha);
	float4 fire = fireGradient.Sample(linearSampler, float2(fireAlpha, 0)) * (1.0f - fireAlpha);
	return fire + smoke;
}